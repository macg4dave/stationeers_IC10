alias tok r0
alias prev r1
alias cmd r2
alias state r3
alias h r4
alias v r5
alias dir r6
alias n r7
alias step r8
alias cond r9
alias sig r10
define CMD_DISCOVER 1
define CMD_CLEAR 3
define MEMORY_HASH HASH("StructureLogicMemory")
define DISH_HASH HASH("StructureLargeSatelliteDish")
define SLOT0_NAME HASH("slot0")
define SLOT1_NAME HASH("slot1")
define SLOT2_NAME HASH("slot2")
define CMD_TOKEN_NAME HASH("cmd_token")
define CMD_TYPE_NAME HASH("cmd_type")
define DISH_NAME HASH("dish")
init:
move prev -1
move state 0
s db Setting 100
main:
yield
lbn cond MEMORY_HASH CMD_TOKEN_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond MEMORY_HASH CMD_TYPE_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond DISH_HASH DISH_NAME PrefabHash Maximum
bne cond DISH_HASH main
lbn tok MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
seq cond tok prev
bnez cond worker
move prev tok
lbn cmd MEMORY_HASH CMD_TYPE_NAME Setting Maximum
beq cmd CMD_DISCOVER start
bne cmd CMD_CLEAR worker
move state 0
sbn MEMORY_HASH SLOT0_NAME Setting 0
sbn MEMORY_HASH SLOT1_NAME Setting 0
sbn MEMORY_HASH SLOT2_NAME Setting 0
s db Setting 140
j main
start:
move state 1
move n 0
move step 0
move h 0
move v 0
move dir 1
sbn MEMORY_HASH SLOT0_NAME Setting 0
sbn MEMORY_HASH SLOT1_NAME Setting 0
sbn MEMORY_HASH SLOT2_NAME Setting 0
s db Setting 110
j main
worker:
beq state 1 do_step
beq state 2 do_sample
j main
do_step:
move cond 10
mul cond cond dir
add cond h cond
bgt cond 359 high
blt cond 0 low
move h cond
j moved
high:
move h 359
move dir -1
j bump
low:
move h 0
move dir 1
j bump
bump:
add v v 10
ble v 89 moved
move v 0
moved:
sbn DISH_HASH DISH_NAME Horizontal h
sbn DISH_HASH DISH_NAME Vertical v
add step step 1
move state 2
s db Setting 120
j main
do_sample:
lbn cond DISH_HASH DISH_NAME Idle Maximum
beqz cond main
sleep 1
lbn sig DISH_HASH DISH_NAME SignalID Maximum
beqz sig sample_done
beq n 0 save0
beq n 1 save1
sbn MEMORY_HASH SLOT2_NAME Setting sig
move n 3
j sample_done
save0:
sbn MEMORY_HASH SLOT0_NAME Setting sig
add n n 1
j sample_done
save1:
sbn MEMORY_HASH SLOT1_NAME Setting sig
add n n 1
j sample_done
sample_done:
beq n 3 full
blt step 72 next_step
beqz n none
s db Setting 132
j stop
full:
s db Setting 130
j stop
none:
s db Setting 131
j stop
next_step:
move state 1
j main
stop:
move state 0
j main