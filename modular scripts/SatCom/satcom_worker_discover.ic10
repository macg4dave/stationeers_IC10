alias tok r0
alias prev r1
alias cmd r2
alias state r3
alias h r4
alias v r5
alias n r6
alias step r7
alias cond r8
alias sig r9
define CMD_DISCOVER 1
define CMD_CLEAR 3
define NO_SIGNAL -1
define MEMORY_HASH HASH("StructureLogicMemory")
define DISH_HASH HASH("StructureLargeSatelliteDish")
define SLOT0_NAME HASH("slot0")
define SLOT1_NAME HASH("slot1")
define SLOT2_NAME HASH("slot2")
define CMD_TOKEN_NAME HASH("cmd_token")
define CMD_TYPE_NAME HASH("cmd_type")
define DISH_NAME HASH("dish")
init:
move prev -1
move state 0
s db Setting 100
main:
yield
lbn cond MEMORY_HASH CMD_TOKEN_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond MEMORY_HASH CMD_TYPE_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond DISH_HASH DISH_NAME PrefabHash Maximum
bne cond DISH_HASH main
lbn tok MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
seq cond tok prev
bnez cond worker
move prev tok
lbn cmd MEMORY_HASH CMD_TYPE_NAME Setting Maximum
beq cmd CMD_DISCOVER start
bne cmd CMD_CLEAR worker
move state 0
sbn MEMORY_HASH SLOT0_NAME Setting NO_SIGNAL
sbn MEMORY_HASH SLOT1_NAME Setting NO_SIGNAL
sbn MEMORY_HASH SLOT2_NAME Setting NO_SIGNAL
s db Setting 140
j main
start:
move state 1
move n 0
move step 0
move h 0
move v 0
sbn DISH_HASH DISH_NAME BestContactFilter -1
lbn r10 MEMORY_HASH SLOT0_NAME Setting Maximum
lbn r11 MEMORY_HASH SLOT1_NAME Setting Maximum
lbn r12 MEMORY_HASH SLOT2_NAME Setting Maximum
sbn MEMORY_HASH SLOT0_NAME Setting NO_SIGNAL
sbn MEMORY_HASH SLOT1_NAME Setting NO_SIGNAL
sbn MEMORY_HASH SLOT2_NAME Setting NO_SIGNAL
s db Setting 110
j main
worker:
beq state 1 do_step
beq state 2 do_sample
j main
do_step:
add h h 10
blt h 360 moved
move h 0
add v v 10
blt v 90 moved
move v 0
moved:
sbn DISH_HASH DISH_NAME Horizontal h
sbn DISH_HASH DISH_NAME Vertical v
add step step 1
move state 2
s db Setting 120
j main
do_sample:
lbn cond DISH_HASH DISH_NAME Idle Maximum
beqz cond main
sleep 5
lbn sig DISH_HASH DISH_NAME SignalID Maximum
beq sig NO_SIGNAL sample_done
beq sig r10 sample_done
beq sig r11 sample_done
beq sig r12 sample_done
beq n 0 save0
beq n 1 save1
sbn MEMORY_HASH SLOT2_NAME Setting sig
move n 3
j sample_done
save0:
sbn MEMORY_HASH SLOT0_NAME Setting sig
add n n 1
j sample_done
save1:
sbn MEMORY_HASH SLOT1_NAME Setting sig
add n n 1
j sample_done
sample_done:
beq n 3 full
blt step 324 next_step
slt cond n 2
bnez cond none_found
s db Setting 132
j stop_cycle
full:
s db Setting 130
j stop_cycle
none_found:
s db Setting 131
j stop
next_step:
move state 1
j main
stop_cycle:
lbn tok MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
add tok tok 1
sbn MEMORY_HASH CMD_TYPE_NAME Setting 2
sbn MEMORY_HASH CMD_TOKEN_NAME Setting tok
stop:
move state 0
j main