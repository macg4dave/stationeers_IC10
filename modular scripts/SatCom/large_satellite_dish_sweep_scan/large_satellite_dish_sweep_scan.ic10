alias dish d0
alias start_btn d1
alias scan_enabled r0
alias pressed r1
alias signal_id r5
alias cond r6
alias tmp r7
alias h_curr r8
alias v_curr r9
alias h_dir r10
alias last_signal_id r12
alias missed_count r14
define H_MIN 0
define H_MAX 359
define H_STEP 10
define V_MIN 0
define V_MAX 89
define V_STEP 10
define STOP_ON_STRENGTH 1
define SIGNAL_STRENGTH_STOP -10
define TARGET_SIGNAL_ID 0
define REACQUIRE_MISSES 6
init:
move scan_enabled 0
move last_signal_id 0
move missed_count 0
move r11 0
move r13 0
move r15 0
move h_curr H_MIN
move v_curr V_MIN
move h_dir 1
main:
yield
bdns dish main
bdns start_btn main
l pressed start_btn Activate
beqz pressed skip_toggle
seq cond scan_enabled 0
select scan_enabled cond 1 0
beqz scan_enabled toggled_off
move missed_count 0
beqz TARGET_SIGNAL_ID clear_filter
s dish BestContactFilter TARGET_SIGNAL_ID
j skip_toggle
toggled_off:
move missed_count 0
clear_filter:
s dish BestContactFilter -1
skip_toggle:
beqz scan_enabled main
l r2 dish Power
beqz r2 main
l r3 dish Error
bnez r3 main
l r4 dish Idle
beqz r4 main
j no_signal
found:
move r15 r13
move r13 r11
move r11 last_signal_id
move last_signal_id signal_id
move scan_enabled 0
bdns db main
s db Setting signal_id
j main
no_signal:
move tmp H_STEP
mul tmp tmp h_dir
add tmp h_curr tmp
bgt tmp H_MAX over_max
blt tmp H_MIN under_min
move h_curr tmp
j do_move
over_max:
move h_curr H_MAX
move h_dir -1
j bump_vertical
under_min:
move h_curr H_MIN
move h_dir 1
j bump_vertical
bump_vertical:
add v_curr v_curr V_STEP
ble v_curr V_MAX do_move
move v_curr V_MIN
do_move:
s dish Horizontal h_curr
s dish Vertical v_curr
wait_idle:
l r4 dish Idle
beqz r4 wait_idle
sleep 1
l signal_id dish SignalID
beqz TARGET_SIGNAL_ID any_signal
seq cond signal_id TARGET_SIGNAL_ID
beqz cond no_signal
j target_strength
any_signal:
snez cond signal_id
beqz cond lost_signal
seq cond signal_id last_signal_id
move missed_count 0
bnez cond no_signal
seq cond signal_id r11
bnez cond no_signal
seq cond signal_id r13
bnez cond no_signal
seq cond signal_id r15
bnez cond no_signal
j found
lost_signal:
add missed_count missed_count 1
blt missed_count REACQUIRE_MISSES no_signal
s dish BestContactFilter -1
move missed_count 0
j no_signal
target_strength:
beqz STOP_ON_STRENGTH found
l tmp dish SignalStrength
sge cond tmp SIGNAL_STRENGTH_STOP
beqz cond no_signal
j found
