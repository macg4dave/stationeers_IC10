# SatCom setup guard (name auto-naming)
# names: cmd/channels + discover/clear/dials + worker housings (see Setup.md)
alias cond r0
alias housing_hash r1
alias inited r2
define MEMORY_HASH HASH("StructureLogicMemory")
define BUTTON_HASH 491845673
define LEVER_HASH 1220484876
define DIAL_HASH 554524804
define DISH_HASH -449434216
define CMD_TOKEN_NAME HASH("cmd_token")
define CMD_TYPE_NAME HASH("cmd_type")
define SLOT0_NAME HASH("slot0")
define SLOT1_NAME HASH("slot1")
define SLOT2_NAME HASH("slot2")
define DISH_NAME HASH("dish")
define DISCOVER_NAME HASH("discover")
define CYCLE_NAME HASH("cycle")
define MANUAL_ENABLE_NAME HASH("manual_enable")
define DIAL_H_NAME HASH("dial_h")
define DIAL_V_NAME HASH("dial_v")
define DISCOVER_WORKER_NAME HASH("discover_worker")
define CONTROLS_WORKER_NAME HASH("controls_worker")
init:
l housing_hash db PrefabHash
move inited 0
s db Setting 0
main:
yield
lbn cond MEMORY_HASH CMD_TOKEN_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_cmd_token
lbn cond MEMORY_HASH CMD_TYPE_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_cmd_type
lbn cond BUTTON_HASH DISCOVER_NAME PrefabHash Maximum
bne cond BUTTON_HASH err_discover_type
lbn cond BUTTON_HASH CYCLE_NAME PrefabHash Maximum
bne cond BUTTON_HASH err_cycle_type
lbn cond LEVER_HASH MANUAL_ENABLE_NAME PrefabHash Maximum
bne cond LEVER_HASH err_dials
lbn cond DIAL_HASH DIAL_H_NAME PrefabHash Maximum
bne cond DIAL_HASH err_dials
lbn cond DIAL_HASH DIAL_V_NAME PrefabHash Maximum
bne cond DIAL_HASH err_dials
lbn cond MEMORY_HASH SLOT0_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond MEMORY_HASH SLOT1_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond MEMORY_HASH SLOT2_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond DISH_HASH DISH_NAME PrefabHash Maximum
bne cond DISH_HASH err_dish
lbn cond housing_hash DISCOVER_WORKER_NAME PrefabHash Maximum
bne cond housing_hash err_discover_h
lbn cond housing_hash CONTROLS_WORKER_NAME PrefabHash Maximum
bne cond housing_hash err_controls_h
lbn cond housing_hash DISCOVER_WORKER_NAME On Maximum
bnez cond chk_controls_on
sbn housing_hash DISCOVER_WORKER_NAME On 1
chk_controls_on:
lbn cond housing_hash CONTROLS_WORKER_NAME On Maximum
bnez cond maybe_init
sbn housing_hash CONTROLS_WORKER_NAME On 1
maybe_init:
bnez inited ok
init_cmd:
lbn cond MEMORY_HASH CMD_TYPE_NAME Setting Maximum
beqz cond init_token
sbn MEMORY_HASH CMD_TYPE_NAME Setting 0
init_token:
lbn cond MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
beqz cond init_slot0
sbn MEMORY_HASH CMD_TOKEN_NAME Setting 0
init_slot0:
lbn cond MEMORY_HASH SLOT0_NAME Setting Maximum
beq cond -1 init_slot1
sbn MEMORY_HASH SLOT0_NAME Setting -1
init_slot1:
lbn cond MEMORY_HASH SLOT1_NAME Setting Maximum
beq cond -1 init_slot2
sbn MEMORY_HASH SLOT1_NAME Setting -1
init_slot2:
lbn cond MEMORY_HASH SLOT2_NAME Setting Maximum
beq cond -1 mark_inited
sbn MEMORY_HASH SLOT2_NAME Setting -1
mark_inited:
move inited 1
ok:
s db Setting 1
j main
err_cmd_token:
s db Setting 91
j main
err_cmd_type:
s db Setting 92
j main
err_discover_h:
s db Setting 93
j main
err_discover_type:
s db Setting 94
j main
err_cycle_type:
s db Setting 95
j main
err_controls_h:
s db Setting 90
j main
err_dish:
s db Setting 97
j main
err_slots:
s db Setting 98
j main
err_dials:
s db Setting 99
j main
