# SatCom setup guard (name auto-naming)
# names: cmd_token cmd_type discover cycle discover_worker cycle_worker
alias cond r0
alias housing_hash r1
alias inited r2
define MEMORY_HASH HASH("StructureLogicMemory")
define BUTTON_HASH 491845673
define DISH_HASH HASH("StructureLargeSatelliteDish")
define CMD_TOKEN_NAME HASH("cmd_token")
define CMD_TYPE_NAME HASH("cmd_type")
define SLOT0_NAME HASH("slot0")
define SLOT1_NAME HASH("slot1")
define SLOT2_NAME HASH("slot2")
define DISH_NAME HASH("dish")
define DISCOVER_NAME HASH("discover")
define CYCLE_NAME HASH("cycle")
define DISCOVER_WORKER_NAME HASH("discover_worker")
define CYCLE_WORKER_NAME HASH("cycle_worker")
init:
l housing_hash db PrefabHash
move inited 0
s db Setting 0
main:
yield
lbn cond MEMORY_HASH CMD_TOKEN_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_cmd_token
lbn cond MEMORY_HASH CMD_TYPE_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_cmd_type
lbn cond BUTTON_HASH DISCOVER_NAME PrefabHash Maximum
bne cond BUTTON_HASH err_discover_type
lbn cond BUTTON_HASH CYCLE_NAME PrefabHash Maximum
bne cond BUTTON_HASH err_cycle_type
lbn cond MEMORY_HASH SLOT0_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond MEMORY_HASH SLOT1_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond MEMORY_HASH SLOT2_NAME PrefabHash Maximum
bne cond MEMORY_HASH err_slots
lbn cond DISH_HASH DISH_NAME PrefabHash Maximum
bne cond DISH_HASH err_dish
lbn cond housing_hash DISCOVER_WORKER_NAME PrefabHash Maximum
bne cond housing_hash err_discover_h
lbn cond housing_hash CYCLE_WORKER_NAME PrefabHash Maximum
bne cond housing_hash err_cycle_h
lbn cond housing_hash DISCOVER_WORKER_NAME On Maximum
bnez cond chk_cycle_on
sbn housing_hash DISCOVER_WORKER_NAME On 1
chk_cycle_on:
lbn cond housing_hash CYCLE_WORKER_NAME On Maximum
bnez cond maybe_init
sbn housing_hash CYCLE_WORKER_NAME On 1
maybe_init:
bnez inited ok
init_cmd:
lbn cond MEMORY_HASH CMD_TYPE_NAME Setting Maximum
beqz cond init_token
sbn MEMORY_HASH CMD_TYPE_NAME Setting 0
init_token:
lbn cond MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
beqz cond mark_inited
sbn MEMORY_HASH CMD_TOKEN_NAME Setting 0
mark_inited:
move inited 1
ok:
s db Setting 1
j main
err_cmd_token:
s db Setting 91
j main
err_cmd_type:
s db Setting 92
j main
err_discover_h:
s db Setting 93
j main
err_discover_type:
s db Setting 94
j main
err_cycle_type:
s db Setting 95
j main
err_cycle_h:
s db Setting 96
j main
err_dish:
s db Setting 97
j main
err_slots:
s db Setting 98
j main
