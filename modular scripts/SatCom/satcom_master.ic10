alias pressed_discover r0
alias pressed_cycle r1
alias prev_discover r2
alias prev_cycle r3
alias edge_discover r4
alias edge_cycle r5
alias token r6
alias cond r7
alias discover_status r8
alias cycle_status r9
alias housing_hash r10
define CMD_DISCOVER 1
define CMD_CYCLE 2
define CMD_CLEAR 3
define BUTTON_HASH 491845673
define MEMORY_HASH HASH("StructureLogicMemory")
define DISCOVER_BTN_NAME HASH("discover")
define CYCLE_BTN_NAME HASH("cycle")
define DISCOVER_WORKER_NAME HASH("discover_worker")
define CYCLE_WORKER_NAME HASH("cycle_worker")
define CMD_TOKEN_NAME HASH("cmd_token")
define CMD_TYPE_NAME HASH("cmd_type")
init:
move prev_discover 0
move prev_cycle 0
move token 0
l housing_hash db PrefabHash
s db Setting 0
main:
yield
lbn cond housing_hash DISCOVER_WORKER_NAME PrefabHash Maximum
bne cond housing_hash main
lbn cond housing_hash CYCLE_WORKER_NAME PrefabHash Maximum
bne cond housing_hash main
lbn cond MEMORY_HASH CMD_TOKEN_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond MEMORY_HASH CMD_TYPE_NAME PrefabHash Maximum
bne cond MEMORY_HASH main
lbn cond housing_hash DISCOVER_WORKER_NAME On Maximum
bnez cond check_cycle_on
sbn housing_hash DISCOVER_WORKER_NAME On 1
check_cycle_on:
lbn cond housing_hash CYCLE_WORKER_NAME On Maximum
bnez cond read_buttons
sbn housing_hash CYCLE_WORKER_NAME On 1
read_buttons:
lbn pressed_discover BUTTON_HASH DISCOVER_BTN_NAME Activate Maximum
lbn cond 1668452680 DISCOVER_BTN_NAME Activate Maximum
or pressed_discover pressed_discover cond
lbn pressed_cycle BUTTON_HASH CYCLE_BTN_NAME Activate Maximum
lbn cond 1668452680 CYCLE_BTN_NAME Activate Maximum
or pressed_cycle pressed_cycle cond
read_edges:
beqz pressed_discover discover_low
beqz prev_discover discover_rise
move edge_discover 0
j discover_done
discover_rise:
move edge_discover 1
move prev_discover 1
j discover_done
discover_low:
move prev_discover 0
move edge_discover 0
discover_done:
beqz pressed_cycle cycle_low
beqz prev_cycle cycle_rise
move edge_cycle 0
j route
cycle_rise:
move edge_cycle 1
move prev_cycle 1
j route
cycle_low:
move prev_cycle 0
move edge_cycle 0
route:
or cond edge_discover edge_cycle
beqz cond publish
and cond pressed_discover pressed_cycle
bnez cond send_clear
bnez edge_discover send_discover
bnez edge_cycle send_cycle
publish:
lbn discover_status housing_hash DISCOVER_WORKER_NAME Setting Maximum
lbn cycle_status housing_hash CYCLE_WORKER_NAME Setting Maximum
beq discover_status 110 discover_busy
beq discover_status 120 discover_busy
beq discover_status 130 discover_ready
beq discover_status 132 discover_ready
beq discover_status 131 discover_none
beq cycle_status 210 cycle_busy
beq cycle_status 220 cycle_ready
s db Setting 1
j main
discover_busy:
s db Setting 2
j main
discover_ready:
s db Setting 5
j main
discover_none:
s db Setting 6
j main
cycle_busy:
s db Setting 3
j main
cycle_ready:
s db Setting 4
j main
send_discover:
move cond CMD_DISCOVER
s db Setting 10
j send_cmd
send_cycle:
move cond CMD_CYCLE
s db Setting 20
j send_cmd
send_clear:
move cond CMD_CLEAR
s db Setting 30
send_cmd:
lbn token MEMORY_HASH CMD_TOKEN_NAME Setting Maximum
add token token 1
sbn MEMORY_HASH CMD_TYPE_NAME Setting cond
sbn MEMORY_HASH CMD_TOKEN_NAME Setting token
j main
