# Purpose: Solar tracking with night-park and optional
#          per-panel orientation (free-form placement).
# Devices: d0 = Daylight Sensor (face up)
# Notes:
# - Label panels by the direction their POWER port faces
#   to enable per-panel horizontals (optional).
# - If all panels are labeled, PanelPortDirection can be
#   ignored (daylight sensor direction still matters).

# --- Configure for your build before use ---
# Port direction: 0=North, 1=East, 2=South, 3=West
# For 2-port panels, use the POWER port direction.
define SensorPortDirection 0 # Daylight Sensor
define PanelPortDirection 0 # Solar Panels
# Label names for panels by port direction (degrees).
# Ensure each direction name is unique.
move r1 HASH("Solar Panel 0")   #0 (North)
move r2 HASH("Solar Panel 90")  #90 (East)
move r3 HASH("Solar Panel 180") #180 (South)
move r4 HASH("Solar Panel 270") #270 (West)
# -- No configuration beyond this point. --
# Note r1-r8 store hashes for quick dereferencing.
alias dDaylightSensor d0
alias rIsNight r11
alias rVertA r12
alias rHorzA r13
alias rSensorAngle r14
alias rPanelAngle r15
move r5 HASH("StructureSolarPanel")
move r6 HASH("StructureSolarPanelDual")
move r7 HASH("StructureSolarPanelReinforced")
move r8 HASH("StructureSolarPanelDualReinforced")
# Init horizontal rotation offset for panel placement
mul rPanelAngle PanelPortDirection -90
# Solar panel vertical axis is perpendicular to port
add rPanelAngle rPanelAngle -90
# Keep offset within -180 to 180
add rPanelAngle rPanelAngle 180
mod rPanelAngle rPanelAngle 360
sub rPanelAngle rPanelAngle 180
# Init daylight sensor rotation offset
mul rSensorAngle SensorPortDirection 90
# Config check
bltz SensorPortDirection ConfigError
bgt SensorPortDirection 3 ConfigError
bltz PanelPortDirection ConfigError
bgt PanelPortDirection 3 ConfigError
bdns dDaylightSensor SensorError #Check if sensor
l r0 dDaylightSensor PrefabHash #connected properly
bne r0 HASH("StructureDaylightSensor") SensorError
s db Setting 0

Cycle:
yield
# Get updated angle
l rVertA dDaylightSensor Vertical
# If abs angle > 90, sun is below horizon.
abs rIsNight rVertA #95 to catch last rays.
sgt rIsNight rIsNight 95 #1=night, 0=day
# DS zero is straight up, but solar panels zero is
sub rVertA 90 rVertA #on the horizon. Convert here.
# Clamp to solar panel bounds, just in case
max rVertA rVertA 15
min rVertA rVertA 90

# If night, reset to morning start position.
move rHorzA -90 #Morning start (East)
bnez rIsNight SkipHereAtNight
l rHorzA dDaylightSensor Horizontal
add rHorzA rHorzA rSensorAngle #Adjust sensor rotation
SkipHereAtNight:
add rHorzA rHorzA rPanelAngle #For global setting.
sb r5 Vertical rVertA #Write angles to panels.
sb r5 Horizontal rHorzA #Vertical is needed here
sb r6 Vertical rVertA #even when using named panels
sb r6 Horizontal rHorzA #for different horizontals.
sb r7 Vertical rVertA
sb r7 Horizontal rHorzA
sb r8 Vertical rVertA
sb r8 Horizontal rHorzA
# Adjust horizontal for NESW labeled panels
sub rHorzA rHorzA rPanelAngle #Take that back off.
move r10 0 #These loops save 16 sb commands,
move r0 rHorzA #plus a bunch of additional math!
LoopOuter:
add r10 r10 1
add r0 r0 -90
move r9 4
LoopInner:
add r9 r9 1 #This runs 16 times. rr10 and rr9
sbn rr9 rr10 Horizontal r0 #become r1-r4 & r5-r8
blt r9 8 LoopInner
blt r10 4 LoopOuter
j Cycle #Back to start


# Daylight sensor pin not assigned, fix and reset IC
SensorError:
s db Setting 9001
s db Error 1 #Read-only, so it throws an error. ;)
# Invalid value in "port direction" settings
ConfigError:
s db Setting 8001
s db Error 1