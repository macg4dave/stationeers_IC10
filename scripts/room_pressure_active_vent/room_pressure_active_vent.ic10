# room_pressure_active_vent (compact)

alias sensor d0
alias vent d1

alias p r0
alias cond r1
alias state r2

define STATE_IDLE 0
define STATE_INTAKE 1
define STATE_EXHAUST 2

define LOW_PRESSURE_KPA 90
define HIGH_PRESSURE_KPA 120
define MODE_OUTWARD 0
define MODE_INWARD 1

define LOOP_SLEEP_S 5

wait:
yield
bdns d0 wait
bdns d1 wait

move state STATE_IDLE
j stop

main:
l p sensor Pressure

# if we're currently intaking, keep locked until we reach LOW_PRESSURE_KPA
seq cond state STATE_INTAKE
beqz cond check_exhaust
sge cond p LOW_PRESSURE_KPA
bnez cond stop
yield
j main

check_exhaust:
# if we're currently exhausting, keep locked until we reach HIGH_PRESSURE_KPA
seq cond state STATE_EXHAUST
beqz cond decide
sle cond p HIGH_PRESSURE_KPA
bnez cond stop
yield
j main

decide:

# below low threshold => intake
slt cond p LOW_PRESSURE_KPA
bnez cond do_intake

# above high threshold => exhaust
sgt cond p HIGH_PRESSURE_KPA
bnez cond do_exhaust

# between thresholds
seq cond state STATE_IDLE
bnez cond idle
j stop

idle:
sleep LOOP_SLEEP_S
j main

do_intake:
move state STATE_INTAKE

# lock, set mode, then set pressures (mode reset warning)
s vent Lock 1
s vent Mode MODE_OUTWARD
s vent Open 1
s vent On 1

yield
j main

do_exhaust:
move state STATE_EXHAUST

# lock, set mode, then set pressures (mode reset warning)
s vent Lock 1
s vent Mode MODE_INWARD
s vent Open 1
s vent On 1

yield
j main

stop:
move state STATE_IDLE
s vent On 0
s vent Open 0
s vent Lock 0

sleep LOOP_SLEEP_S
j main